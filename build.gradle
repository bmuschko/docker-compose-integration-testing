plugins {
    id 'java'
    id 'com.gradle.build-scan' version '1.13.1'
    id 'com.avast.gradle.docker-compose' version '0.7.1'
}

sourceCompatibility = 9
targetCompatibility = 9

buildScan {
    termsOfServiceUrl = 'https://gradle.com/terms-of-service'
    termsOfServiceAgree = 'yes'
    publishAlways()
}

dockerCompose {
    useComposeFiles = ['docker-compose.yml']
    isRequiredBy(project.tasks.test)
}

def httpclientModuleJvmArg = '--add-modules=jdk.incubator.httpclient'

compileTestJava {
    options.compilerArgs.add(httpclientModuleJvmArg)
}

test {
    useJUnitPlatform()
    jvmArgs httpclientModuleJvmArg
}

repositories {
    mavenCentral()
}

dependencies {
    def junitJupiterVersion = '5.1.1'
    testImplementation "org.junit.jupiter:junit-jupiter-api:$junitJupiterVersion"
    testImplementation "org.junit.jupiter:junit-jupiter-params:$junitJupiterVersion"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junitJupiterVersion"
}